def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('key.properties')
if (keyPropertiesFile.exists()) {
    keyProperties.load(new FileInputStream(keyPropertiesFile))
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def dartEnvironmentVariables = [:]
if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = dartEnvironmentVariables + project.property('dart-defines')
            .split(',')
            .collectEntries { entry ->
                def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
                [(pair.first()): pair.last()]
            }
}

task copyGoogleServicesJson(type: Copy) {
    from "src/${dartEnvironmentVariables.ENV_FLAVOR}/google-services.json"
    into './'
}

tasks.whenTaskAdded { task ->
    if (task.name == 'processDebugGoogleServices') {
        task.dependsOn copyGoogleServicesJson
    }
    if (task.name == 'processReleaseGoogleServices') {
        task.dependsOn copyGoogleServicesJson
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        resValue "string", "app_name", dartEnvironmentVariables.ENV_APP_NAME ? dartEnvironmentVariables.ENV_APP_NAME : "hittick"
        resValue "string", "facebook_app_id", dartEnvironmentVariables.ENV_FACEBOOK_APP_ID ? dartEnvironmentVariables.ENV_FACEBOOK_APP_ID : ""
        resValue "string", "facebook_client_token", dartEnvironmentVariables.ENV_FACEBOOK_APP_SECRET ? dartEnvironmentVariables.ENV_FACEBOOK_APP_SECRET : ""
        applicationId "com.chaomao.hittick"
        minSdkVersion 21
        targetSdkVersion 33
        multiDexEnabled true
        versionCode 33
        versionName "1.2.10"
    }

    signingConfigs {
        debug {
            keyAlias keyProperties['debugKeyAlias']
            keyPassword keyProperties['debugKeyPassword']
            storeFile keyProperties['debugStoreFile'] ? file(keyProperties['debugStoreFile']) : null
            storePassword keyProperties['debugStorePassword']
        }
        release {
            keyAlias keyProperties['releaseKeyAlias']
            keyPassword keyProperties['releaseKeyPassword']
            storeFile keyProperties['releaseStoreFile'] ? file(keyProperties['releaseStoreFile']) : null
            storePassword keyProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'
    implementation platform('com.google.firebase:firebase-bom:26.1.0')
}
